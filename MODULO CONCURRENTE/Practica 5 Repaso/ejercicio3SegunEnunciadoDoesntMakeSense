programa ejercicio3
procesos
  proceso juntarFlor(ES flor: numero)
  comenzar
    si(HayFlorEnLaEsquina)
      tomarFlor
      flor:= flor + 1
  fin
  proceso tirarFlores
  comenzar
    mientras(HayFlorEnLaBolsa)
      depositarFlor
  fin
  proceso recorrer
  variables
    pasos, flor: numero
  comenzar
    pasos:= 0
    flor:= 0
    mientras(((pasos <> 5) & (flor <> 10)) & (PosAv < 100))
      juntarFlor(flor)
      mover
      pasos:= pasos + 1
  fin
  proceso depositar(E avInicial: numero; E caInicial: numero)
  comenzar
    BloquearEsquina(10,10)
    Pos(10,10)
    tirarFlores
    Pos(avInicial, caInicial)
    LiberarEsquina(10,10)
  fin
  proceso juntarFlores(ES flores: numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      flores:= flores + 1
  fin
  proceso buscarFlores(ES flores: numero; E av: numero; E ca: numero)
  comenzar
    BloquearEsquina(10,10)
    Pos(10,10)
    juntarFlores(flores)
    Pos(av,ca)
    LiberarEsquina(10,10)
  fin
areas
  areaCompartida : AreaC(10,10,10,10)
  areaRecolector1 : AreaP(1,3,100,3)
  areaRecolector2 : AreaP(1,4,100,4)
  areaCosechador : AreaP(1,5,1,5)
robots
  robot recolector
  variables
    avInicial, caInicial: numero
  comenzar
    derecha
    mientras(PosAv < 100)
      recorrer
      avInicial:= PosAv
      caInicial:= PosCa
      depositar(avInicial, caInicial)
      EnviarMensaje(1, robot3)
    EnviarMensaje(0, robot3)
  fin
  robot cosechador
  variables
    cantFinish, estado, flores: numero
  comenzar
    flores:= 0
    cantFinish:= 0
    RecibirMensaje(estado, *)
    mientras(cantFinish <> 2)
      si(estado = 1)
        buscarFlores(flores, PosAv, PosCa)
      sino
        cantFinish:= cantFinish + 1
      si(cantFinish <> 2)
        RecibirMensaje(estado, *)
    Informar(flores)    
  fin
variables
  robot1: recolector
  robot2: recolector
  robot3: cosechador
comenzar
  AsignarArea(robot1, areaCompartida)
  AsignarArea(robot1, areaRecolector1)
  AsignarArea(robot2, areaCompartida)
  AsignarArea(robot2, areaRecolector2)
  AsignarArea(robot3, areaCompartida)
  AsignarArea(robot3, areaCosechador)
  Iniciar(robot1, 1,3)
  Iniciar(robot2, 1,4)
  Iniciar(robot3, 1,5)
fin  